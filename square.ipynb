{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "3e8c4b19",
   "metadata": {},
   "outputs": [],
   "source": [
    "using JuMP\n",
    "# using Ipopt\n",
    "using MadNLP\n",
    "using ProgressMeter\n",
    "using CairoMakie"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "c1f4c8d4",
   "metadata": {},
   "outputs": [],
   "source": [
    "num_triangs_hor = 4 # choose even\n",
    "num_triangs_ver = 4 # choose even\n",
    "num_triangs = 2*num_triangs_hor * num_triangs_ver\n",
    "num_edges = num_triangs_hor * (num_triangs_ver + 1) + num_triangs_ver * (2*num_triangs_hor + 1)\n",
    "num_free_verts_hor = num_triangs_hor - 1 # ignore leftmost and rightmost vertices\n",
    "num_free_verts_ver = num_triangs_ver + 1\n",
    "num_verts_hor = num_free_verts_hor + 2\n",
    "num_verts_ver = num_free_verts_ver\n",
    "fps = 120\n",
    "animation_fps = 60\n",
    "animation_scale = 4\n",
    "triang_side_length = 1\n",
    "triang_height = sqrt(3)/2 * triang_side_length\n",
    "pref_dist = triang_side_length\n",
    "break_dist = 2*pref_dist\n",
    "start_width = num_triangs_hor*triang_side_length\n",
    "max_pull = 1.4*break_dist - pref_dist\n",
    "max_push = 0.6*pref_dist\n",
    "min_x = 0\n",
    "max_x = start_width + max_pull + triang_side_length/2\n",
    "min_y = -triang_height\n",
    "max_y = (num_triangs_ver + 1)*triang_height\n",
    "pot_min = -1.\n",
    "diss_coeff = 0.5\n",
    "time_pull = 2.\n",
    "l2_dissipation = true # if false => Kelvin-Voigt\n",
    "soft_max_alpha = 5\n",
    "time_horizon = 5*time_pull\n",
    "animation_width = 800\n",
    "animation_aspect = (max_x - min_x) / (max_y - min_y)\n",
    "animation_height = animation_width / animation_aspect\n",
    "dotsize = 6\n",
    "linewidth = 1\n",
    "hidpi_scaling = 2\n",
    "file_name = \"square\"\n",
    "fontsize = 16\n",
    "only_video = true; # if false generate a folder of snapshots for each frame"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "3efcc884",
   "metadata": {},
   "outputs": [],
   "source": [
    "function lennard_jones(dist_sq)\n",
    "    # Lennard-jones of squared distances.\n",
    "    # The factor cbrt(2) assures that the global minimum \n",
    "    # is attained at cur_dist == pref_dist\n",
    "    q = pref_dist^2/(cbrt(2)*dist_sq)\n",
    "    -4*pot_min*(q^6 - q^3)\n",
    "    # alternative power\n",
    "    # q = pref_dist^2/(2*dist_sq)\n",
    "    # -4*pot_min*(q^2 - q)\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "906f05fb",
   "metadata": {},
   "outputs": [],
   "source": [
    "function dirichlet(t)\n",
    "    t = t % (4*time_pull)\n",
    "    if t <= 2*time_pull\n",
    "        return start_width + max_pull*sin(pi/2*t/time_pull)\n",
    "    else\n",
    "        return start_width + max_push*sin(pi/2*t/time_pull)\n",
    "    end\n",
    "end\n",
    "\n",
    "ts = range(0, time_horizon, 1000)\n",
    "lines(ts, dirichlet.(ts))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "a9f0aec6",
   "metadata": {},
   "outputs": [],
   "source": [
    "function get_edges(verts)\n",
    "    edges = Matrix{Tuple{Any, Any}}(undef, num_edges, 2)\n",
    "    edge_id = 1\n",
    "    # add horizontal edges\n",
    "    for i in 1:num_verts_hor-1\n",
    "        for j in 1:num_verts_ver\n",
    "            edges[edge_id, 1] = verts[i,j]\n",
    "            edges[edge_id, 2] = verts[i+1,j]\n",
    "            edge_id += 1\n",
    "        end\n",
    "    end\n",
    "    # right-leaning odd rows\n",
    "    for i in 1:num_verts_hor\n",
    "        for j in 1:2:num_verts_ver-1\n",
    "            edges[edge_id, 1] = verts[i,j]\n",
    "            edges[edge_id, 2] = verts[i,j+1]\n",
    "            edge_id += 1\n",
    "        end\n",
    "    end\n",
    "    # right-leaning even rows\n",
    "    for i in 1:num_verts_hor-1\n",
    "        for j in 2:2:num_verts_ver-1\n",
    "            edges[edge_id, 1] = verts[i,j]\n",
    "            edges[edge_id, 2] = verts[i+1,j+1]\n",
    "            edge_id += 1\n",
    "        end\n",
    "    end\n",
    "    # left-leaning odd rows\n",
    "    for i in 2:num_verts_hor\n",
    "        for j in 1:2:num_verts_ver-1\n",
    "            edges[edge_id, 1] = verts[i,j]\n",
    "            edges[edge_id, 2] = verts[i-1,j+1]\n",
    "            edge_id += 1\n",
    "        end\n",
    "    end\n",
    "    # left-leaning even rows\n",
    "    for i in 1:num_verts_hor\n",
    "        for j in 2:2:num_verts_ver-1\n",
    "            edges[edge_id, 1] = verts[i,j]\n",
    "            edges[edge_id, 2] = verts[i,j+1]\n",
    "            edge_id += 1\n",
    "        end\n",
    "    end \n",
    "    edges\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "78026672",
   "metadata": {},
   "outputs": [],
   "source": [
    "function get_triangles(verts)\n",
    "    triangs = Matrix{Tuple{Any, Any}}(undef, num_triangs, 3)\n",
    "    triang_id = 1\n",
    "    # upward-pointing odd rows\n",
    "    for i in 1:num_verts_hor-1\n",
    "        for j in 1:2:num_verts_ver-1\n",
    "            triangs[triang_id, 1] = verts[i,j]\n",
    "            triangs[triang_id, 2] = verts[i+1,j]\n",
    "            triangs[triang_id, 3] = verts[i,j+1]\n",
    "            triang_id += 1\n",
    "        end\n",
    "    end\n",
    "    # upward-pointing even rows\n",
    "    for i in 1:num_verts_hor-1\n",
    "        for j in 2:2:num_verts_ver-1\n",
    "            triangs[triang_id, 1] = verts[i,j]\n",
    "            triangs[triang_id, 2] = verts[i+1,j]\n",
    "            triangs[triang_id, 3] = verts[i+1,j+1]\n",
    "            triang_id += 1\n",
    "        end\n",
    "    end\n",
    "    # downward-pointing odd rows\n",
    "    for i in 1:num_verts_hor-1\n",
    "        for j in 3:2:num_verts_ver\n",
    "            triangs[triang_id, 1] = verts[i,j]\n",
    "            triangs[triang_id, 2] = verts[i,j-1]\n",
    "            triangs[triang_id, 3] = verts[i+1,j]\n",
    "            triang_id += 1\n",
    "        end\n",
    "    end\n",
    "    # downward-pointing odd rows\n",
    "    for i in 1:num_verts_hor-1\n",
    "        for j in 2:2:num_verts_ver\n",
    "            triangs[triang_id, 1] = verts[i,j]\n",
    "            triangs[triang_id, 2] = verts[i+1,j-1]\n",
    "            triangs[triang_id, 3] = verts[i+1,j]\n",
    "            triang_id += 1\n",
    "        end\n",
    "    end\n",
    "    triangs\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "3359f477",
   "metadata": {},
   "outputs": [],
   "source": [
    "function vector_to_values(vec)\n",
    "    broadcast((p) -> value.(p), vec)\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "6f9c8a7f",
   "metadata": {},
   "outputs": [],
   "source": [
    "function plot_step!(ax, vertices, edges, triangles)\n",
    "    plot_triangles!(ax, triangles)\n",
    "    plot_edges!(ax, edges)\n",
    "    plot_vertices!(ax, vertices)\n",
    "end\n",
    "\n",
    "function plot_step(step)\n",
    "    fig = Figure(\n",
    "        resolution=(\n",
    "            animation_scale*animation_width,\n",
    "            animation_scale*animation_height\n",
    "        ), fontsize=animation_scale*fontsize\n",
    "    )\n",
    "    ax = Axis(\n",
    "        fig[1, 1],\n",
    "        limits=(min_x-0.25, max_x+0.25, min_y, max_y),\n",
    "        aspect=animation_aspect,\n",
    "    )\n",
    "    \n",
    "    plot_step!(ax, vector_to_values.(step)...)\n",
    "\n",
    "    fig\n",
    "end\n",
    "\n",
    "function plot_vertices!(ax, vertices)\n",
    "    vertices = vertices[:]\n",
    "    x = [v[1] for v in vertices]\n",
    "    y = [v[2] for v in vertices]\n",
    "    scatter!(ax, x, y, color=:blue, markersize=animation_scale*dotsize)\n",
    "end\n",
    "\n",
    "function plot_edges!(ax, edges)\n",
    "    num_edges = size(edges)[1]\n",
    "    for i in 1:num_edges\n",
    "        v1, v2 = edges[i, :]\n",
    "        x1, y1 = v1\n",
    "        x2, y2 = v2\n",
    "        lines!(\n",
    "            ax, [x1, x2], [y1, y2],\n",
    "            color=:green,\n",
    "            linewidth=animation_scale*linewidth,\n",
    "#             linestyle=:dash\n",
    "        )\n",
    "    end\n",
    "end\n",
    "\n",
    "function plot_triangles!(ax, triangles)\n",
    "    num_triags = size(triangles)[1]\n",
    "\n",
    "    for i in 1:num_triags\n",
    "        poly!(ax, Tuple.(triangles[i, :]), color=(:pink, 0.5))\n",
    "    end\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "e3143f53",
   "metadata": {},
   "outputs": [],
   "source": [
    "minmove = Model(\n",
    "    ()->MadNLP.Optimizer(\n",
    "        print_level=MadNLP.WARN,\n",
    "        blas_num_threads=8,\n",
    "        # acceptable_tol=1e-8,\n",
    "        # max_iter=1000\n",
    "    )\n",
    ")\n",
    "\n",
    "# minmove = Model(Ipopt.Optimizer)\n",
    "# set_optimizer_attribute(minmove, \"max_cpu_time\", 60.0)\n",
    "# set_optimizer_attribute(minmove, \"print_level\", 0)\n",
    "\n",
    "step = 2\n",
    "prev_L = num_triangs_hor*triang_side_length\n",
    "L = dirichlet((step-1)/fps)\n",
    "\n",
    "@NLparameter(\n",
    "    minmove,\n",
    "    prev_x[i=1:num_free_verts_hor,j=1:num_free_verts_ver] ==\n",
    "        i*triang_side_length + ((j+1) % 2)*triang_side_length/2\n",
    ")\n",
    "@NLparameter(\n",
    "    minmove,\n",
    "    prev_y[i=1:num_free_verts_hor,j=1:num_free_verts_ver] ==\n",
    "        (j-1)*triang_height\n",
    ")\n",
    "\n",
    "delta_L = L - prev_L\n",
    "# apply uniform correction\n",
    "@variable(\n",
    "    minmove,\n",
    "    min_x <= x[i=1:num_free_verts_hor,j=1:num_free_verts_ver] <= max_x,\n",
    "    start = value(prev_x[i,j]) + delta_L/num_free_verts_hor\n",
    ")\n",
    "@variable(\n",
    "    minmove,\n",
    "    min_y <= y[i=1:num_free_verts_hor,j=1:num_free_verts_ver] <= max_y,\n",
    "    start = value(prev_y[i,j])\n",
    ")\n",
    "\n",
    "# Dirichlet condition\n",
    "@NLparameter(\n",
    "    minmove,\n",
    "    prev_bdry_x[j=1:num_free_verts_ver] == prev_L + ((j+1) % 2)*triang_side_length/2\n",
    ")\n",
    "@NLparameter(\n",
    "    minmove,\n",
    "    bdry_x[j=1:num_free_verts_ver] == L + ((j+1) % 2)*triang_side_length/2\n",
    ")\n",
    "\n",
    "# vertices \n",
    "prev_vertices = Matrix{Any}(undef, num_verts_hor, num_verts_ver)\n",
    "vertices = Matrix{Any}(undef, num_verts_hor, num_verts_ver)\n",
    "# leftmost (fixed)\n",
    "for j in 1:num_verts_ver\n",
    "    prev_vertices[1,j] = (\n",
    "        ((j+1) % 2)*triang_side_length/2,\n",
    "        (j-1)*triang_height\n",
    "    )\n",
    "    vertices[1,j] = prev_vertices[1,j]\n",
    "end\n",
    "# middle (free)\n",
    "for i in 1:num_free_verts_hor\n",
    "    for j in 1:num_free_verts_ver\n",
    "        prev_vertices[i+1,j] = (\n",
    "            prev_x[i,j],\n",
    "            prev_y[i,j]\n",
    "        )\n",
    "        vertices[i+1,j] = (\n",
    "            x[i,j],\n",
    "            y[i,j]\n",
    "        )\n",
    "    end\n",
    "end\n",
    "# rightmost (driven by the boundary condition)\n",
    "for j in 1:num_verts_ver\n",
    "    prev_vertices[num_verts_hor, j] = (\n",
    "        prev_bdry_x[j],\n",
    "        (j-1)*triang_height\n",
    "    )\n",
    "    vertices[num_verts_hor, j] = (\n",
    "        bdry_x[j],\n",
    "        (j-1)*triang_height\n",
    "    )\n",
    "end\n",
    "\n",
    "prev_edges = get_edges(prev_vertices)\n",
    "edges = get_edges(vertices)\n",
    "\n",
    "prev_triangles = get_triangles(prev_vertices)\n",
    "triangles = get_triangles(vertices)\n",
    "\n",
    "@NLexpression(\n",
    "    minmove,\n",
    "    dissipation,\n",
    "    sum(\n",
    "        (x[i,j]-prev_x[i,j])^2 + (y[i,j]-prev_y[i,j])^2\n",
    "        for i in 1:num_free_verts_hor, j in 1:num_free_verts_ver\n",
    "    )\n",
    ")\n",
    "\n",
    "@expression(minmove, v1[i=1:num_edges], edges[i,1])\n",
    "@expression(minmove, v2[i=1:num_edges], edges[i,2])\n",
    "@NLexpression(\n",
    "    minmove,\n",
    "    dist_sq[i=1:num_edges],\n",
    "    (v2[i][1] - v1[i][1])^2 + (v2[i][2] - v1[i][2])^2\n",
    ")\n",
    "# we implicitely assume that the simulation starts\n",
    "# in a globally optimal state\n",
    "@NLparameter(minmove, max_dist_sq[i=1:num_edges] == pref_dist)\n",
    "@NLparameter(minmove, broken[i=1:num_edges] == 0.)\n",
    "\n",
    "register(minmove, :W, 1, lennard_jones, autodiff = true)\n",
    "# no memory for the moment\n",
    "@NLexpression(\n",
    "    minmove,\n",
    "    energy,\n",
    "    sum((1-broken[i])*W(dist_sq[i]) + broken[i]*(\n",
    "        (\n",
    "            W(dist_sq[i])*exp(soft_max_alpha*W(dist_sq[i])) +\n",
    "            W(max_dist_sq[i])*exp(soft_max_alpha*W(max_dist_sq[i]))\n",
    "        ) / (exp(soft_max_alpha*W(dist_sq[i])) + exp(soft_max_alpha*W(max_dist_sq[i])))\n",
    "    ) for i in 1:num_edges)\n",
    ")\n",
    "\n",
    "@NLobjective(\n",
    "    minmove,\n",
    "    Min,\n",
    "    energy + diss_coeff*fps*dissipation\n",
    ")\n",
    "\n",
    "optimize!(minmove)\n",
    "\n",
    "steps = [vector_to_values.((prev_vertices, prev_edges, prev_triangles))]\n",
    "push!(steps, vector_to_values.((vertices, edges, triangles)));"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "db54975e",
   "metadata": {},
   "outputs": [],
   "source": [
    "plot_step(steps[2])"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "2d583626",
   "metadata": {},
   "outputs": [],
   "source": [
    "@showprogress \"Computing minmoves...\" for step in 3:fps*time_horizon\n",
    "    # update memory variables\n",
    "    last_dist_sq = value.(dist_sq)\n",
    "    new_max_dist_sq = max.(value.(max_dist_sq), last_dist_sq)\n",
    "    set_value.(max_dist_sq, new_max_dist_sq)\n",
    "    \n",
    "    # check for broken links\n",
    "    new_broken = new_max_dist_sq .> break_dist^2\n",
    "    set_value.(broken, new_broken)\n",
    "    \n",
    "    # new -> old\n",
    "    set_value.(prev_y, value.(y))\n",
    "    set_value.(prev_x, value.(x))\n",
    "    prev_L = L\n",
    "    L = dirichlet((step-1)/fps)\n",
    "    delta_L = L - prev_L\n",
    "    \n",
    "    # update right boundary\n",
    "    set_value.(prev_bdry_x, value.(bdry_x))\n",
    "    set_value.(bdry_x, value.(bdry_x) .+ delta_L)\n",
    "\n",
    "    # update start values\n",
    "    new_start_x = value.(x) .+ delta_L/num_free_verts_hor\n",
    "    new_start_y = value.(y)\n",
    "    set_start_value.(x, new_start_x)\n",
    "    set_start_value.(y, new_start_y)\n",
    "\n",
    "    optimize!(minmove)\n",
    "\n",
    "    push!(steps, vector_to_values.((vertices, edges, triangles)))\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "930a21dc",
   "metadata": {},
   "outputs": [],
   "source": [
    "length(steps)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "2a0029d9",
   "metadata": {},
   "outputs": [],
   "source": [
    "sum(value.(broken))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "092949c5",
   "metadata": {},
   "outputs": [],
   "source": [
    "plot_step(steps[400])"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "125611d4",
   "metadata": {},
   "outputs": [],
   "source": [
    "function animate_steps(steps)\n",
    "    experiments_folder = \"experiments\"\n",
    "    if !ispath(experiments_folder)\n",
    "        mkdir(experiments_folder)\n",
    "    end\n",
    "    if !only_video\n",
    "        snapshots_folder = \"$experiments_folder/$file_name\"\n",
    "        if !ispath(snapshots_folder)\n",
    "            mkdir(snapshots_folder)\n",
    "        end\n",
    "    end\n",
    "    \n",
    "    fig = Figure(\n",
    "        resolution=(\n",
    "            animation_scale*animation_width,\n",
    "            animation_scale*animation_height\n",
    "        ), fontsize=animation_scale*fontsize\n",
    "    )\n",
    "    ax = Axis(\n",
    "        fig[1, 1],\n",
    "        limits=(min_x-0.25, max_x+0.25, min_y, max_y),\n",
    "        aspect=animation_aspect,\n",
    "    )\n",
    "    \n",
    "    frame = 1\n",
    "    record(fig, \"experiments/$file_name.mp4\", steps; framerate=animation_fps) do (step)\n",
    "        empty!(ax)\n",
    "        plot_step!(ax, step...)\n",
    "        if !only_video\n",
    "            save(\"$snapshots_folder/$(file_name)_$frame.png\", fig)\n",
    "        end\n",
    "        frame += 1\n",
    "    end\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "229c4041",
   "metadata": {},
   "outputs": [],
   "source": [
    "# animate_steps(steps[1:4:end])"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "3cc8cd5c",
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.7.2",
   "language": "julia",
   "name": "julia-1.7"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.7.2"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
