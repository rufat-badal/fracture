{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "36fbc912",
   "metadata": {},
   "outputs": [],
   "source": [
    "using JuMP\n",
    "using MadNLP\n",
    "using ProgressMeter\n",
    "using CairoMakie"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "27f672d6",
   "metadata": {},
   "outputs": [],
   "source": [
    "num_atoms = 15\n",
    "num_bonds = num_atoms - 1\n",
    "fps = 60\n",
    "animation_fps = 60\n",
    "animation_scale = 4\n",
    "pref_dist = 1.\n",
    "start_L = num_bonds*pref_dist\n",
    "break_dist = 1.8*pref_dist\n",
    "max_pull = 2*break_dist - pref_dist\n",
    "max_push = 1.5*pref_dist\n",
    "min_crack = 0.025*pref_dist\n",
    "max_crack = start_L + max_pull - (num_bonds-1)*min_crack\n",
    "pot_min = -1.\n",
    "diss_coeff = 0.3\n",
    "time_pull = 3.\n",
    "l2_dissipation = false # if false => Kelvin-Voigt\n",
    "soft_max_alpha = 5\n",
    "time_horizon = 6*time_pull\n",
    "animation_width = 800\n",
    "hidpi_scaling = 2\n",
    "fontsize = 16\n",
    "only_video = true\n",
    "experiments_folder = \"experiments\";"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "be79b498",
   "metadata": {},
   "outputs": [],
   "source": [
    "function lennard_jones(dist_sq)\n",
    "    # Lennard-jones of squared distances.\n",
    "    # The factor cbrt(2) assures that the global minimum \n",
    "    # is attained at cur_dist == pref_dist\n",
    "    q = pref_dist^2/(cbrt(2)*dist_sq)\n",
    "    -4*pot_min*(q^6 - q^3)\n",
    "#     q = pref_dist^2/(2*dist_sq)\n",
    "#     -4*pot_min*(q^2 - q)\n",
    "end\n",
    "\n",
    "function dirichlet_delta(t)\n",
    "    if t <= 2*time_pull\n",
    "        return max_pull*sin(pi/2*t/time_pull)\n",
    "    elseif t <= 4*time_pull\n",
    "        return max_push*sin(pi/2*t/time_pull)\n",
    "    elseif t <= 5*time_pull\n",
    "        return max_pull*sin(pi/2*t/time_pull)\n",
    "    else\n",
    "        return max_pull\n",
    "    end\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "61755803",
   "metadata": {},
   "outputs": [],
   "source": [
    "function compute_line_minmoves()\n",
    "    step = 2\n",
    "\n",
    "    minmove = Model(\n",
    "        ()->MadNLP.Optimizer(\n",
    "            print_level=MadNLP.WARN,\n",
    "            blas_num_threads=2,\n",
    "            # acceptable_tol=1e-8,\n",
    "            # max_iter=1000\n",
    "        )\n",
    "    )\n",
    "\n",
    "    prev_L = start_L\n",
    "    L = start_L + dirichlet_delta((step-1)/fps)\n",
    "    # uniform distribution of atoms at the start of the simulation\n",
    "    @NLparameter(\n",
    "        minmove, \n",
    "        prev_delta_y[i=1:num_bonds] == prev_L / num_bonds\n",
    "    )\n",
    "    @NLexpression(\n",
    "        minmove, prev_y[i=1:num_atoms],\n",
    "        sum(prev_delta_y[j] for j in 1:i-1),\n",
    "    )\n",
    "\n",
    "    @NLparameter(minmove, max_elong_sq[i=1:num_bonds] == value(prev_delta_y[i])^2)\n",
    "    @NLparameter(minmove, broken[i=1:num_bonds] == 0.)\n",
    "\n",
    "    delta_L = L - prev_L\n",
    "    # initial guess: uniform expansion of all bonds\n",
    "    @variable(\n",
    "        minmove, min_crack <= delta_y[i=1:num_bonds] <= max_crack,\n",
    "        start=value(prev_delta_y[i]) + delta_L/num_bonds\n",
    "    )\n",
    "    @expression(minmove, y[i=1:num_atoms], sum(delta_y[j] for j in 1:i-1))\n",
    "\n",
    "    # Dirichlet boundary condition\n",
    "    @constraint(minmove, dirichlet_right, y[end] == L)\n",
    "\n",
    "    # Energy\n",
    "    @expression(minmove, dist_sq[i=1:num_bonds], delta_y[i]^2)\n",
    "    register(minmove, :W, 1, lennard_jones, autodiff = true)\n",
    "    @NLexpression(\n",
    "        minmove, energy_expr,\n",
    "        sum((1-broken[i])*W(dist_sq[i]) + broken[i]*(\n",
    "            (\n",
    "                W(dist_sq[i])*exp(soft_max_alpha*W(dist_sq[i])) +\n",
    "                W(max_elong_sq[i])*exp(soft_max_alpha*W(max_elong_sq[i]))\n",
    "            ) / (exp(soft_max_alpha*W(dist_sq[i])) + exp(soft_max_alpha*W(max_elong_sq[i])))\n",
    "        ) for i in 1:num_bonds)\n",
    "    )\n",
    "\n",
    "    # Dissipation\n",
    "    if l2_dissipation\n",
    "        @NLexpression(\n",
    "            minmove, dissipation_expr,\n",
    "            sum((y[i] - prev_y[i])^2 for i in 2:num_atoms-1)\n",
    "        )\n",
    "    else\n",
    "        @NLexpression(\n",
    "            minmove, dissipation_expr,\n",
    "            sum((delta_y[i] - prev_delta_y[i])^2 for i in 1:num_bonds)\n",
    "        )\n",
    "    end\n",
    "\n",
    "    @NLobjective(minmove, Min, energy_expr + diss_coeff*fps*dissipation_expr)\n",
    "\n",
    "    optimize!(minmove)\n",
    "    \n",
    "    steps = [value.(prev_y), value.(y)]\n",
    "    \n",
    "    @showprogress \"Computing minmoves...\" for step in 3:fps*time_horizon\n",
    "        # update memory variable\n",
    "        y_last = steps[end]\n",
    "        delta_y_last = y_last[2:end] - y_last[1:end-1]\n",
    "        comp_max_elong_sq = delta_y_last.^2\n",
    "        new_max_elong_sq = max.(value.(max_elong_sq), comp_max_elong_sq)\n",
    "        set_value.(max_elong_sq, new_max_elong_sq)\n",
    "    \n",
    "        # check for broken links\n",
    "        new_broken = new_max_elong_sq .> break_dist^2\n",
    "        set_value.(broken, new_broken)\n",
    "\n",
    "        # new -> old\n",
    "        # note: delta_y is the variable not y\n",
    "        set_value.(prev_delta_y, value.(delta_y_last))\n",
    "        prev_L = L\n",
    "        L = start_L + dirichlet_delta((step-1)/fps)\n",
    "\n",
    "        # update rod length\n",
    "        set_normalized_rhs(dirichlet_right, L)\n",
    "\n",
    "        # update start value\n",
    "        delta_L = L - prev_L\n",
    "        set_start_value.(delta_y, delta_y_last .+ delta_L/num_bonds)\n",
    "\n",
    "        optimize!(minmove)\n",
    "\n",
    "        push!(steps, value.(y))\n",
    "    end\n",
    "    \n",
    "    steps\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "7a097b5b",
   "metadata": {},
   "outputs": [],
   "source": [
    "steps = compute_line_minmoves();"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "fb569d8b",
   "metadata": {},
   "outputs": [],
   "source": [
    "function setup_line_fig()  \n",
    "    aspect = 4\n",
    "    ax_aspect = 4\n",
    "    width = animation_width\n",
    "    height = width/aspect\n",
    "    max_right = maximum([y[end] for y in steps])\n",
    "    min_left = minimum([y[1] for y in steps])\n",
    "    delta = 0.25\n",
    "    max_len = max_right - min_left + 2delta\n",
    "    ax_height = max_len/ax_aspect\n",
    "    \n",
    "    fig = Figure(\n",
    "        resolution=(animation_scale*width, animation_scale*height),\n",
    "        fontsize=animation_scale*fontsize\n",
    "    )\n",
    "    ax = Axis(\n",
    "        fig[1, 1],\n",
    "        limits=(min_left-delta, max_right+delta, -ax_height/2, ax_height/2),\n",
    "        aspect=ax_aspect,\n",
    "    )\n",
    "    \n",
    "    fig, ax\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "35c5f8b7",
   "metadata": {},
   "outputs": [],
   "source": [
    "function animate_line_steps(steps, file_name)\n",
    "    if !ispath(experiments_folder)\n",
    "        mkdir(experiments_folder)\n",
    "    end\n",
    "    if !only_video\n",
    "        snapshots_folder = \"$experiments_folder/$file_name\"\n",
    "        if !ispath(snapshots_folder)\n",
    "            mkdir(snapshots_folder)\n",
    "        end\n",
    "    end\n",
    "    \n",
    "    fig, ax = setup_line_fig()\n",
    "    \n",
    "    N = length(steps[1])\n",
    "    y = zeros(N)\n",
    "    frame = 1\n",
    "    record(fig, \"experiments/$file_name.mp4\", steps; framerate = animation_fps) do (x)\n",
    "        empty!(ax)\n",
    "        scatter!(x, y, color=:blue, markersize=animation_scale*6)\n",
    "        if !only_video\n",
    "            save(\"$snapshots_folder/$(file_name)_$frame.png\", fig)\n",
    "        end\n",
    "        frame += 1\n",
    "    end\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "20d7d03d",
   "metadata": {},
   "outputs": [],
   "source": [
    "animate_line_steps(steps, \"line\")"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "5f22384e",
   "metadata": {},
   "outputs": [],
   "source": [
    "function plot_line_step(step, file_name)\n",
    "    if !ispath(experiments_folder)\n",
    "        mkdir(experiments_folder)\n",
    "    end\n",
    "    \n",
    "    fig, ax = setup_line_fig()\n",
    "    \n",
    "    N = length(step)\n",
    "    x = step\n",
    "    y = zeros(N)\n",
    "    \n",
    "    scatter!(x, y, color=:blue, markersize=animation_scale*6)\n",
    "    save(\"$experiments_folder/$file_name.pdf\", fig)\n",
    "    \n",
    "    fig\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "311eb81c",
   "metadata": {},
   "outputs": [],
   "source": [
    "plot_line_step(steps[50], \"line_step\")"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "a91c6f4f",
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.7.2",
   "language": "julia",
   "name": "julia-1.7"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.7.2"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
