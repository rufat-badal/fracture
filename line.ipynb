{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "using JuMP\n",
    "using MadNLP\n",
    "using ProgressMeter\n",
    "using CairoMakie"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "num_atoms = 15\n",
    "num_bonds = num_atoms - 1\n",
    "fps = 60\n",
    "animation_fps = 60\n",
    "animation_scale = 4\n",
    "pref_dist = 1.\n",
    "init_L = num_bonds*pref_dist\n",
    "break_dist = 2*pref_dist\n",
    "max_L = (num_bonds-1)*pref_dist + 1.6*break_dist\n",
    "max_pull_perc = max_L / init_L - 1.\n",
    "min_crack = 0.1*pref_dist\n",
    "max_crack = max_L - (num_bonds-1)*min_crack\n",
    "pot_min = -1.\n",
    "diss_coeff = 0.3\n",
    "time_pull = 4.\n",
    "l2_dissipation = false # if false => Kelvin-Voigt\n",
    "soft_max_alpha = 5\n",
    "time_horizon = 6*time_pull\n",
    "animation_width = 800\n",
    "hidpi_scaling = 2\n",
    "file_name = \"line\"\n",
    "fontsize = 16\n",
    "only_video = true; # if false generate a folder of snapshots for each frame"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function lennard_jones(dist_sq; pot_min=-1)\n",
    "    # Lennard-jones of squared distances.\n",
    "    # The factor cbrt(2) assures that the global minimum \n",
    "    # is attained at cur_dist == pref_dist\n",
    "    q = pref_dist^2/(cbrt(2)*dist_sq)\n",
    "    -4*pot_min*(q^6 - q^3)\n",
    "#     q = pref_dist^2/(2*dist_sq)\n",
    "#     -4*pot_min*(q^2 - q)\n",
    "end\n",
    "xs = range(0.72*pref_dist^2, 5*pref_dist^2, length=1000)\n",
    "W(dsq) = lennard_jones(dsq, pot_min=pot_min)\n",
    "lines(xs, W.(xs))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function dirichlet_delta(t)\n",
    "    if t <= 2*time_pull\n",
    "        return max_pull_perc*init_L*sin(pi/2*t/time_pull)\n",
    "    else\n",
    "        return 0\n",
    "    end\n",
    "end\n",
    "\n",
    "function dirichlet_delta_compress(t)\n",
    "    if t <= 3*time_pull\n",
    "        return max_pull_perc*init_L*sin(pi/2*t/time_pull)\n",
    "    else\n",
    "        return -max_pull_perc*init_L\n",
    "    end\n",
    "end\n",
    "\n",
    "function dirichlet_delta_twice(t)\n",
    "    if t <= 5*time_pull\n",
    "        return max_pull_perc*init_L*sin(pi/2*t/time_pull)\n",
    "    else\n",
    "        return max_pull_perc*init_L\n",
    "    end\n",
    "end\n",
    "\n",
    "g(t) = init_L + dirichlet_delta_twice(t)\n",
    "\n",
    "xs = range(0, time_horizon, length=1000)\n",
    "lines(xs, g.(xs), color=:blue)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Task:\n",
    "\n",
    "Given an initial length of the rod compute the minimal configuration of the atoms.\n",
    "(No dissipation is used here.)\n",
    "\n",
    "We try several initial values:\n",
    "- uniform distribution\n",
    "- crack at bond i"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function plot_rod!(ax, delta_y; shift=0., color=:blue)\n",
    "    y = [0.]\n",
    "    for dy in delta_y\n",
    "        push!(y, y[end]+dy)\n",
    "    end\n",
    "    \n",
    "    scatter!(ax, y, shift*ones(length(y)), color=color, markersize=10)\n",
    "end\n",
    "fig = Figure()\n",
    "ax = Axis(fig[1, 1], aspect=3)\n",
    "plot_rod!(ax, pref_dist*ones(num_bonds))\n",
    "plot_rod!(ax, pref_dist*ones(num_bonds), shift=0.1, color=:red)\n",
    "fig"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "L = 2*num_bonds*pref_dist\n",
    "broken_bond = 2\n",
    "max_crack = L - (num_bonds-1)*pref_dist\n",
    "sigma = pref_dist/100\n",
    "\n",
    "function get_start_delta_y(i; favor=-1, L=2*num_bonds*pref_dist)\n",
    "    uniform_dist = L / num_bonds\n",
    "    \n",
    "    if favor == -1\n",
    "        return uniform_dist\n",
    "    end\n",
    "    \n",
    "    diff = pref_dist/10\n",
    "    if i != favor\n",
    "        return uniform_dist - diff/(num_bonds-1)\n",
    "    else\n",
    "        return uniform_dist + diff\n",
    "    end\n",
    "end\n",
    "\n",
    "function get_noisy_start_delta_y(sigma)\n",
    "    uniform_dist = L / num_bonds\n",
    "    start_delta_y = [uniform_dist + sigma*(2*rand()-1) for _ in 1:num_bonds]\n",
    "    new_L = sum(start_delta_y)\n",
    "    (L/new_L)*start_delta_y\n",
    "end\n",
    "\n",
    "# delta_y_start = [get_start_delta_y(i, favor=broken_bond, L=L) for i in 1:num_bonds]\n",
    "# delta_y_start = get_noisy_start_delta_y(sigma)\n",
    "delta_y_start = [get_start_delta_y(i, favor=broken_bond, L=L) for i in 1:num_bonds]\n",
    "\n",
    "rate_independent = Model(\n",
    "    ()->MadNLP.Optimizer(\n",
    "        print_level=MadNLP.WARN,\n",
    "        # acceptable_tol=1e-8,\n",
    "        # max_iter=1000\n",
    "    )\n",
    ")\n",
    "\n",
    "@variable(\n",
    "    rate_independent,\n",
    "    0 <= delta_y[i=1:num_bonds] <= max_crack,\n",
    "    start=delta_y_start[i]\n",
    ")\n",
    "\n",
    "@constraint(\n",
    "    rate_independent, lenght_L,\n",
    "    sum(delta_y[i] for i in 1:num_bonds) == L\n",
    ")\n",
    "\n",
    "register(rate_independent, :W, 1, lennard_jones, autodiff = true)\n",
    "@NLobjective(\n",
    "    rate_independent, Min, \n",
    "    sum(W(delta_y[i]^2) for i in 1:num_bonds)\n",
    ")\n",
    "\n",
    "optimize!(rate_independent)\n",
    "\n",
    "empty!(ax)\n",
    "plot_rod!(ax, delta_y_start)\n",
    "plot_rod!(ax, value.(delta_y), shift=1, color=:red)\n",
    "fig"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Try a global minimization without starting value.\n",
    "(Of course this is much harder to optimize.)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "L = 100*num_bonds*pref_dist\n",
    "max_crack = L - (num_bonds-1)*pref_dist\n",
    "\n",
    "rate_independent = Model(\n",
    "    ()->MadNLP.Optimizer(\n",
    "        print_level=MadNLP.WARN,\n",
    "        # acceptable_tol=1e-8,\n",
    "        # max_iter=1000\n",
    "    )\n",
    ")\n",
    "\n",
    "# no start value\n",
    "@variable(rate_independent, 0 <= delta_y[i=1:num_bonds] <= max_crack)\n",
    "\n",
    "@constraint(\n",
    "    rate_independent, lenght_L,\n",
    "    sum(delta_y[i] for i in 1:num_bonds) == L\n",
    ")\n",
    "\n",
    "register(rate_independent, :W, 1, lennard_jones, autodiff = true)\n",
    "@NLobjective(\n",
    "    rate_independent, Min, \n",
    "    sum(W(delta_y[i]^2) for i in 1:num_bonds)\n",
    ")\n",
    "\n",
    "optimize!(rate_independent) # Does not find the correct global minimum!\n",
    "\n",
    "empty!(ax)\n",
    "plot_rod!(ax, value.(delta_y))\n",
    "println(value.(delta_y))\n",
    "fig"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "step = 2\n",
    "\n",
    "minmove = Model(\n",
    "    ()->MadNLP.Optimizer(\n",
    "        print_level=MadNLP.WARN,\n",
    "        blas_num_threads=8,\n",
    "        # acceptable_tol=1e-8,\n",
    "        # max_iter=1000\n",
    "    )\n",
    ")\n",
    "\n",
    "prev_L = init_L\n",
    "L = g((step-1)/fps)\n",
    "# uniform distribution of atoms at the start of the simulation\n",
    "@NLparameter(\n",
    "    minmove, \n",
    "    prev_delta_y[i=1:num_bonds] == prev_L / num_bonds\n",
    ")\n",
    "@NLexpression(\n",
    "    minmove, prev_y[i=1:num_atoms],\n",
    "    sum(prev_delta_y[j] for j in 1:i-1),\n",
    ")\n",
    "\n",
    "@NLparameter(minmove, max_elong_sq[i=1:num_bonds] == value(prev_delta_y[i])^2)\n",
    "@NLparameter(minmove, broken[i=1:num_bonds] == 0.)\n",
    "\n",
    "delta_L = L - prev_L\n",
    "# initial guess: uniform expansion of all bonds\n",
    "@variable(\n",
    "    minmove, min_crack <= delta_y[i=1:num_bonds] <= max_crack,\n",
    "    start=value(prev_delta_y[i]) + delta_L/num_bonds\n",
    ")\n",
    "@expression(minmove, y[i=1:num_atoms], sum(delta_y[j] for j in 1:i-1))\n",
    "\n",
    "# Dirichlet boundary condition\n",
    "@constraint(minmove, dirichlet_right, y[end] == L)\n",
    "\n",
    "# Energy\n",
    "@expression(minmove, dist_sq[i=1:num_bonds], delta_y[i]^2)\n",
    "register(minmove, :W, 1, lennard_jones, autodiff = true)\n",
    "@NLexpression(\n",
    "    minmove, energy_expr,\n",
    "    sum((1-broken[i])*W(dist_sq[i]) + broken[i]*(\n",
    "        (\n",
    "            W(dist_sq[i])*exp(soft_max_alpha*W(dist_sq[i])) +\n",
    "            W(max_elong_sq[i])*exp(soft_max_alpha*W(max_elong_sq[i]))\n",
    "        ) / (exp(soft_max_alpha*W(dist_sq[i])) + exp(soft_max_alpha*W(max_elong_sq[i])))\n",
    "    ) for i in 1:num_bonds)\n",
    ")\n",
    "\n",
    "# Dissipation\n",
    "if l2_dissipation\n",
    "    @NLexpression(\n",
    "        minmove, dissipation_expr,\n",
    "        .5*sum((y[i] - prev_y[i])^2 for i in 2:num_atoms-1)\n",
    "    )\n",
    "else\n",
    "    @NLexpression(\n",
    "        minmove, dissipation_expr,\n",
    "        .5*sum((delta_y[i] - prev_delta_y[i])^2 for i in 1:num_bonds)\n",
    "    )\n",
    "end\n",
    "\n",
    "@NLobjective(minmove, Min, energy_expr + diss_coeff*fps*dissipation_expr)\n",
    "\n",
    "optimize!(minmove)\n",
    "\n",
    "value.(y)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function dissipation(y, prev_y)\n",
    "    num_atoms = length(y)\n",
    "    .5*sum((y[i] - prev_y[i])^2 for i in 2:num_atoms-1)\n",
    "end\n",
    "dissipation(value.(y), value.(prev_y)), value(dissipation_expr)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function soft_max(x, y; alpha=soft_max_alpha)\n",
    "    (x*exp(alpha*x) + y*exp(alpha*y)) / (exp(alpha*x) + exp(alpha*y))\n",
    "end\n",
    "\n",
    "xs = range(-1.5*break_dist, 1.5*break_dist, length=1000)\n",
    "ys = [max(x, 0) for x in xs]\n",
    "soft_ys = [soft_max(x, 0) for x in xs]\n",
    "fig = Figure()\n",
    "ax = Axis(fig[1,1])\n",
    "lines!(ax, xs, ys, color=:blue)\n",
    "lines!(ax, xs, soft_ys, color=:red)\n",
    "fig"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function energy(y, max_elong_sq)\n",
    "    num_bonds = length(y)-1\n",
    "    dist_sq = (y[2:end]-y[1:end-1]).^2\n",
    "    res = 0.\n",
    "    for i in 1:num_bonds\n",
    "        if max_elong_sq[i] < break_dist^2\n",
    "            res += W(dist_sq[i])\n",
    "        else\n",
    "            res += soft_max(W(dist_sq[i]), W(max_elong_sq[i]))\n",
    "        end\n",
    "    end\n",
    "    res\n",
    "end\n",
    "energy(value.(y), value.(max_elong_sq)), value(energy_expr)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function total(y, prev_y, max_elong_sq)\n",
    "    energy(y, max_elong_sq) + diss_coeff*fps*dissipation(y, prev_y)\n",
    "end\n",
    "\n",
    "total(value.(y), value.(prev_y), value.(max_elong_sq)), objective_value(minmove)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# sanity check\n",
    "delta_y_comp = value.(prev_delta_y) .+ delta_L/num_bonds\n",
    "y_comp = Vector{Float64}(undef, num_atoms)\n",
    "y_comp[1] = 0.\n",
    "for i in 2:num_atoms\n",
    "    y_comp[i] = y_comp[i-1] + delta_y_comp[i-1]\n",
    "end\n",
    "y_comp\n",
    "total(y_comp, value.(prev_y), value.(max_elong_sq))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "steps = [value.(prev_y), value.(y)]"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "fig = Figure()\n",
    "ax = Axis(\n",
    "    fig[1, 1],\n",
    "    aspect=3,\n",
    "    limits=(-0.5, (1 + max_pull_perc)*init_L+0.5, -0.5, 0.5)\n",
    ")\n",
    "fig"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "@showprogress \"Computing minmoves...\" for step in 3:fps*time_horizon\n",
    "    # update memory variable\n",
    "    y_last = steps[end]\n",
    "    delta_y_last = y_last[2:end] - y_last[1:end-1]\n",
    "    comp_max_elong_sq = delta_y_last.^2\n",
    "    new_max_elong_sq = max.(value.(max_elong_sq), comp_max_elong_sq)\n",
    "    set_value.(max_elong_sq, new_max_elong_sq)\n",
    "    \n",
    "    # check for broken links\n",
    "    new_broken = new_max_elong_sq .> break_dist^2\n",
    "    set_value.(broken, new_broken)\n",
    "    \n",
    "    # new -> old\n",
    "    # note: delta_y is the variable not y\n",
    "    set_value.(prev_delta_y, value.(delta_y_last))\n",
    "    prev_L = L\n",
    "    L = g((step-1)/fps)\n",
    "    \n",
    "    # update rod length\n",
    "    set_normalized_rhs(dirichlet_right, L)\n",
    "\n",
    "    # update start value\n",
    "    delta_L = L - prev_L\n",
    "    set_start_value.(delta_y, delta_y_last .+ delta_L / num_bonds)\n",
    "\n",
    "    optimize!(minmove)\n",
    "\n",
    "    push!(steps, value.(y))\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "length(steps)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "value.(broken)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "max_elong_sq"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function animate_steps(\n",
    "    steps;\n",
    "    file_name=file_name,\n",
    "    fps=animation_fps, width=animation_width, fontsize=11, scale=animation_scale, \n",
    "    save_snapshots=!only_video\n",
    ")\n",
    "    experiments_folder = \"experiments\"\n",
    "    if !ispath(experiments_folder)\n",
    "        mkdir(experiments_folder)\n",
    "    end\n",
    "    if save_snapshots\n",
    "        snapshots_folder = \"$experiments_folder/$file_name\"\n",
    "        if !ispath(snapshots_folder)\n",
    "            mkdir(snapshots_folder)\n",
    "        end\n",
    "    end\n",
    "    \n",
    "    aspect = 4\n",
    "    ax_aspect = 4\n",
    "    height = width/aspect\n",
    "    max_right = maximum([y[end] for y in steps])\n",
    "    min_left = minimum([y[1] for y in steps])\n",
    "    delta = 0.1\n",
    "    max_len = max_right - min_left + 2delta\n",
    "    ax_height = max_len/ax_aspect\n",
    "    N = length(steps[1])\n",
    "    \n",
    "    fig = Figure(resolution=(scale*width, scale*height), fontsize=scale*fontsize)\n",
    "    ax = Axis(\n",
    "        fig[1, 1],\n",
    "        limits=(min_left-delta, max_right+delta, -ax_height/2, ax_height/2),\n",
    "        aspect=ax_aspect,\n",
    "    )\n",
    "    \n",
    "    y = zeros(N)\n",
    "    \n",
    "    frame = 1\n",
    "    record(fig, \"experiments/$file_name.mp4\", steps; framerate = fps) do (x)\n",
    "        empty!(ax)\n",
    "        scatter!(x, y, color=:blue, markersize=scale*6)\n",
    "        if save_snapshots\n",
    "            save(\"$snapshots_folder/$(file_name)_$frame.png\", fig)\n",
    "        end\n",
    "        frame += 1\n",
    "    end\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "animate_steps(steps[1:4:end])"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function plot_energy(steps)\n",
    "    ts = [1/fps*(i-1) for i in 1:length(steps)]\n",
    "    es = Vector{Float64}()\n",
    "    max_elong_sq = zeros(num_bonds)\n",
    "    for y in steps\n",
    "        push!(es, energy(y, max_elong_sq))\n",
    "        # update maximal elongation\n",
    "        dist_y_sq = (y[2:end] - y[1:end-1]).^2\n",
    "        max_elong_sq = max.(max_elong_sq, dist_y_sq)\n",
    "    end\n",
    "    lines(ts, es)\n",
    "end\n",
    "plot_energy(steps)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function plot_dissipation(steps)\n",
    "    ts = [1/fps*(i-1) for i in 1:length(steps)]\n",
    "    ds = [0.]\n",
    "    prev_y = steps[1]\n",
    "    for y in steps[2:end]\n",
    "        push!(ds, diss_coeff*fps*dissipation(y, prev_y))\n",
    "        prev_y = y\n",
    "    end\n",
    "    lines(ts, ds)\n",
    "end\n",
    "\n",
    "plot_dissipation(steps)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function plot_total(steps)\n",
    "    ts = [1/fps*(i-1) for i in 1:length(steps)]\n",
    "    tots = Vector{Float64}()\n",
    "    max_elong_sq = zeros(num_bonds)\n",
    "    tots = [energy(steps[1], max_elong_sq)]\n",
    "    prev_y = steps[1]\n",
    "    for y in steps[2:end]\n",
    "        # update maximal elongation\n",
    "        dist_y_sq = (prev_y[2:end] - prev_y[1:end-1]).^2\n",
    "        max_elong_sq = max.(max_elong_sq, dist_y_sq)\n",
    "        push!(tots, energy(y, max_elong_sq) + diss_coeff*fps*dissipation(y, prev_y))\n",
    "        prev_y = y\n",
    "    end\n",
    "    lines(ts, tots)\n",
    "end\n",
    "\n",
    "plot_total(steps)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.7.2",
   "language": "julia",
   "name": "julia-1.7"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.7.2"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
